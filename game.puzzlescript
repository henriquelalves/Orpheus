title Orpheus
author Illusion Fisherman
homepage https://illusion-fisherman.itch.io/
run_rules_on_level_start

========
OBJECTS
========

Background
#4d4b46 #4a4844
00001
00011
00111
01111
11111


Player
#d61718 #ef6062 #001eef #e0e0e0
.....
.....
.....
.....
.....

StartingPlayerMark
red

PlayerAliveCheck
#d61718 #ef6062 #001eef #e0e0e0
.....
.....
.....
.....
.....

PlayerDead
#d61718 #ef6062 #001eef #e0e0e0
.....
.1.1.
..1..
.1.1.
.....

PlayerLeft A
#d61718 #ef6062 #001eef #e0e0e0
.00..
.330.
.232.
.222.
.2.2.

PlayerRight D
#d61718 #ef6062 #001eef #e0e0e0
..00.
.033.
.232.
.222.
.2.2.

PlayerUp W
#d61718 #ef6062 #001eef #e0e0e0
.000.
.222.
32223
.222.
.2.2.

PlayerDown S
#d61718 #ef6062 #001eef #e0e0e0
.000.
.333.
33233
32223
.2.2.

Wall
#a2592f #cab170 #12111f
21111
01001
01101
00002
22222

SeeingWall
#a2592f #cab170 #12111f
21212
02021
21202
02022
22222

SeeingWallTemp
black
.....
.....
.....
.....
.....

SeenWall X
#a2592f #cab170 #12111f #09249f
22222
22222
22322
22222
22222

WallAndEyeLock E
#afaa99 #251a5e #358fc3
.....
.111.
11211
.111.
.....

EyeLock
#fbfcfb #26aacf  #12111f #09249f
20002
00200
22222
00200
20002

EyeUnlock
#fbfcfb #26aacf  #12111f #09249f
00100
01110
11311
01110
00100

Gate G
#fbfcfb #26aacf
0...0
0.0.0
0.0.0
0.0.0
0.0.0

TempGate
yellow

PowerOnTile
#26aacf
00000
00000
00000
00000
00000

PowerOffTile
#12111f
00000
00000
00000
00000
00000

WireUD H
#4d4b46 #4a4844 #444b5c black
00.01
00.11
00.11
01.11
11.11

WireUL J
#4d4b46 #4a4844 #444b5c
00.01
00.11
...11
01111
11111

WireUR K
#4d4b46 #4a4844 #444b5c
00.01
00.11
00...
01111
11111

WireLR L
#4d4b46 #4a4844 #444b5c
00001
00011
.....
01111
11111

WireLD N
#4d4b46 #4a4844 #444b5c
00001
00011
...11
01.11
11.11

WireRD M
#4d4b46 #4a4844 #444b5c
00001
00011
00...
01.11
11.11

WireUDLR
#4d4b46 #4a4844 #444b5c
00.01
00.11
.....
01.11
11.11

=======
LEGEND
=======

. = Background
# = Wall

MovingPlayer = PlayerUp or PlayerDown or PlayerLeft or PlayerRight
Solid = Wall or SeeingWall or SeeingWallTemp or SeenWall or Gate or TempGate
VisionBlocker = Wall or SeeingWall or SeeingWallTemp or SeenWall

Wire = WireUD or WireUL or WireUR or WireLR or WireLD or WireRD or WireUDLR
WireU = WireUD or WireUL or WireUR or WireUDLR
WireL = WireUL or WireLR or WireLD or WireUDLR
WireR = WireUR or WireLR or WireRD or WireUDLR
WireD = WireUD or WireLD or WireRD or WireUDLR

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
PowerOnTile, PowerOffTile
WireUD, WireUL, WireUR, WireLR, WireLD, WireRD, WireUDLR
Player, PlayerDead, Wall, SeenWall, SeeingWall, SeeingWallTemp, Gate, TempGate
PlayerLeft, PlayerRight, PlayerUp, PlayerDown
PlayerAliveCheck
EyeLock, EyeUnlock
WallAndEyeLock
StartingPlayerMark

======
RULES
======


(Stage setup)
[WallAndEyeLock] -> [Wall EyeLock]
[PlayerUp no Player] -> [PlayerUp up Player StartingPlayerMark]
[PlayerDown no Player] -> [PlayerDown down Player StartingPlayerMark]
[PlayerLeft no Player] -> [PlayerLeft left Player StartingPlayerMark]
[PlayerRight no Player] -> [PlayerRight right Player StartingPlayerMark]

[Wire no PowerOnTile no PowerOffTile] -> [Wire PowerOffTile]
[EyeLock no PowerOffTile] -> [EyeLock PowerOffTile WireUDLR]

(Set player movement sprite)
[MovingPlayer] -> [ ]
[right Player] -> [right Player right PlayerRight]
[left Player] -> [left Player left PlayerLeft]
[up Player] -> [up Player up PlayerUp]
[down Player] -> [down Player down PlayerDown]

(Set seen walls)
[SeeingWall] -> [SeeingWallTemp]

[> MovingPlayer | ... | Wall] -> [> MovingPlayer | ... | SeeingWall]
[> MovingPlayer | ... | SeeingWallTemp] -> [> MovingPlayer | ... | SeeingWall]
[> MovingPlayer | ... | VisionBlocker | ... | SeeingWall] -> [> MovingPlayer | ... | VisionBlocker | ... | Wall]

[SeeingWallTemp] -> [SeenWall]

(Stop player on walls)
[> MovingPlayer | Solid ] -> [MovingPlayer | Solid]

(Dead check)
[ moving Player ] -> [ moving PlayerAliveCheck ]
[ moving PlayerAliveCheck | ... | SeeingWall ] -> [ moving Player | ... | SeeingWall ]

[ PlayerAliveCheck ] -> [ PlayerDead ]
[ PlayerDead PlayerLeft ] -> [ PlayerDead ]
[ PlayerDead PlayerRight ] -> [ PlayerDead ]
[ PlayerDead PlayerUp ] -> [ PlayerDead ]
[ PlayerDead PlayerDown ] -> [ PlayerDead ]

(Eye lock)
[ EyeLock SeeingWall] -> [EyeUnlock SeeingWall]
[EyeUnlock PowerOffTile] -> [EyeUnlock PowerOnTile]


startloop
up [ WireU PowerOffTile | WireD PowerOnTile ] -> [ WireU PowerOnTile | WireD PowerOnTile ]
down [ WireD PowerOffTile | WireU PowerOnTile ] -> [ WireD PowerOnTile | WireU PowerOnTile ]
left [ WireL PowerOffTile | WireR PowerOnTile ] -> [ WireL PowerOnTile | WireR PowerOnTile ]
right [ WireR PowerOffTile | WireL PowerOnTile ] -> [ WireR PowerOnTile | WireL PowerOnTile ]
endloop

(Gate)
[ Gate ] -> [ TempGate ]
up [ TempGate | PowerOffTile WireD ] -> [ Gate | PowerOffTile WireD ]
down [ TempGate | PowerOffTile WireU ] -> [ Gate | PowerOffTile WireU ]
left [ TempGate | PowerOffTile WireR ] -> [ Gate | PowerOffTile WireR ]
right [ TempGate | PowerOffTile WireL ] -> [ Gate | PowerOffTile WireL ]
[ TempGate ] -> [ ]

(Start stage setup)
[moving Player moving MovingPlayer StartingPlayerMark] -> [Player MovingPlayer] 

==============
WINCONDITIONS
==============

No EyeLock


=======
LEVELS
=======

MESSAGE yagheossirelmeuq

######
#..s.#
#e#..#
#....#
#...e#
######

#########
#..g#g..#
#.#h.h#.#
#.#hsh#.#
#.ej.ke.#
#e#####e#

#########
#s....e.#
#.e.e.mg#
#.....e.#
#.e..e.e#
#########

(Old stages)
(
#########
#.......#
#.x...#.#
#...#..##
#w...#..#
#########

#######
#.s...#
#mlllle
#h.#e##
#h.#h.#
#g##h.#
#..gj.#
#######

xxxxxxxx
x.x.x..x
x..g.#.x
x..kln.x
x.#sgh.x
ellljh#x
x..#.h.x
xxxxxexx

############
#s.#.......#
#.......#..#
#..##.#....#
#...#....#.#
#.#...#....#
#...#....###
#..#.......#
#......#...#
############

#######
#.s.#.#
#...#g#
#...xh#
#mlnxh#
#g#e.h#
#....h#
#####e#

#########
#.......#
#.x...#.#
#...#..##
#w...#..#
#########

#######
##.g.s#
##ee..#
#.g...#
#######

#########
#......##
#...#e.##
##.#.gg.#
#...#ge.#
#w....#.#
#########
)

MESSAGE Jobi Joba cada dia yo te quiero mas!

